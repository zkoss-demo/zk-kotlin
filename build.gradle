apply plugin: 'war'

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
		classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
		classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
	}
}

apply plugin: "kotlin"
apply plugin: "kotlin-noarg"
apply plugin: "kotlin-allopen"

noArg {
	annotation("zk.kotlin.vm.FormBean")
}
allOpen {
	annotation("zk.kotlin.vm.FormBean")
}

// In this section you declare where to find the dependencies of your project
repositories {
	maven { url "http://mavensync.zkoss.org/maven2" }
	maven { url "http://mavensync.zkoss.org/eval" }
	mavenCentral()
}

sourceSets {
	jettyRunner {
		runtimeClasspath += sourceSets.main.runtimeClasspath
	}
}

// In this section you declare the dependencies for your production and test code
dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib"
	compile "org.jetbrains.kotlin:kotlin-reflect"

	compile "org.zkoss.zk:zkmax:${zkVersion}"
	compile "org.zkoss.zk:zuti:${zkVersion}"

	testCompile "org.zkoss.zats:zats-mimic-ext7:${zatsVersion}"
	testCompile "junit:junit:${junitVersion}"

	jettyRunnerRuntime "org.eclipse.jetty:jetty-runner:${jettyVersion}"
	jettyRunnerRuntime "org.eclipse.jetty.websocket:javax-websocket-server-impl:${jettyVersion}"
	jettyRunnerRuntime "javax.websocket:javax.websocket-api:${websocketApiVersion}"
}

//launch Jetty using jetty-runner
task startJettyRunner(type: JavaExec) {
	classpath = sourceSets.jettyRunner.runtimeClasspath
	main 'org.eclipse.jetty.runner.Runner'
	args '--path', "/${rootProject.name}", 'src/main/webapp'
}
